#!/usr/bin/env lsc

# To compile this file and generate scripts/generate_polymer_dependencies
# npm install -g lscbin
# lscbin scripts/generate_polymer_dependencies.ls

require! {
  fs
  cheerio
  process
  path
  optionator
  glob
}

src_path = path.resolve process.argv[1], '../../src'

options = {}

do ->
  options_list = [
    {
      option: 'tostdout'
      type: 'Boolean'
      default: 'false'
      description: 'Prints result to stdout instead of writing to .deps.js file'
    }
    {
      option: 'verbose'
      type: 'Boolean'
      default: 'false'
      description: 'Prints additional information to stdout'
    }
    {
      option: 'bower'
      type: 'Boolean'
      default: 'false'
      description: 'Include bower_components directory among those we generate .deps.js files for'
    }
    {
      option: 'regenerate'
      type: 'Boolean'
      default: 'true'
      description: 'Regenerates .deps.js files that already exist'
    }
    {
      option: 'recursive'
      type: 'Boolean'
      default: 'true'
      description: 'Generates .deps.js files recursively'
    }
  ]
  option_parser = optionator {
    options: options_list
  }
  options := option_parser.parseArgv(process.argv)

get_abs_path_noerr = (import_path, filepath_abs) ->
  output = path.resolve filepath_abs, import_path
  if fs.existsSync(output)
    return output
  output = path.resolve src_path, import_path
  if fs.existsSync(output)
    return output
  return null

get_abs_path = (import_path, filepath_abs) ->
  output = get_abs_path_noerr import_path, filepath_abs
  if not output?
    console.log "missing file #{import_path} relative to #{filepath_abs} or #{src_path}"
    #process.exit()
  return output

get_abs_path_script = (import_path, filepath_abs) ->
  output = get_abs_path_noerr import_path, filepath_abs
  if not output?
    for extension in ['.ls', '.jsx']
      output = get_abs_path_noerr import_path.replace(/\.js$/, extension), filepath_abs
      if output != null
        break
  if not output?
    console.log "missing file #{import_path} relative to #{filepath_abs} or #{src_path}"
    #process.exit()
  return output

# dependencies for script tag
script_deps = (tag, params) ->
  {filepath_abs, filename_abs, $} = params
  import_path = $(tag).attr('src')
  if not import_path?
    console.log "script tag in #{filename_abs} is missing src attribute"
    return ''
  filename_abs = get_abs_path_script import_path, filepath_abs
  if not filename_abs?
    return ''
  filename_rel = path.relative src_path, filename_abs
  return "require('#{filename_rel}')"

get_html_import_abspath = (tag, params) ->
  {filepath_rel, filepath_abs, $} = params
  import_path = $(tag).attr('href')
  filename_abs = get_abs_path import_path, filepath_abs
  return filename_abs

# dependencies for html imports
html_import_deps = (tag, params) ->
  {filepath_rel, filepath_abs, $} = params
  import_path = $(tag).attr('href')
  filename_abs = get_abs_path import_path, filepath_abs
  if not filename_abs?
    return ''
  filename_rel = path.relative src_path, filename_abs
  output = []
  #output.push "import_dom_modules(require('html!#{relative_path}'))"
  deps_file = filename_rel.replace(/\.html$/, '.deps.js')
  output.push "require('#{deps_file}')"
  return output.join "\n"

generated_during_this_run = {}

generate_dependencies_for_file = (filename_abs) ->
  outfile_abs = filename_abs.replace(/\.html/, '.deps.js')
  if generated_during_this_run[outfile_abs]
    return
  generated_during_this_run[outfile_abs] = true
  if not options.tostdout and fs.existsSync(outfile_abs)
    if not options.regenerate
      return
    fs.unlinkSync(outfile_abs)
  if options.verbose
    console.log "generating: #{outfile_abs}"
  filename_rel = path.relative src_path, filename_abs
  filepath_rel = path.resolve filename_rel, '..'
  filepath_abs = path.resolve filename_abs, '..'
  $ = cheerio.load(fs.readFileSync(filename_abs, 'utf-8'))
  params = {filepath_rel, filepath_abs, filename_abs, $}
  output = []
  dependencies = []
  output.push '// this file was generated by scripts/generate_polymer_dependencies'
  output.push '// do not edit this file directly'
  output.push '// instead, edit the corresponding .html file and re-run the script'
  output.push "const {import_dom_modules} = require('libs_frontend/dom_utils')"
  for tag in $('link[rel="import"]')
    dependency_file = get_html_import_abspath(tag, params)
    if not dependency_file?
      console.log "html import does not exist for #{filename_abs}"
      continue
    dependencies.push dependency_file
    output.push html_import_deps(tag, params)
  output.push "import_dom_modules(require('html!#{filename_rel}'))"
  for tag in $('script')
    output.push script_deps(tag, params)
  output = output.join("\n").split("\n").filter(-> it.length > 0).join("\n") + "\n"
  if options.tostdout
    console.log output
    return
  fs.writeFileSync outfile_abs, output
  if options.recursive
    for dep in dependencies
      generate_dependencies_for_file(dep)
  return

do ->
  filename = options['_'][0] # first positional argument
  if filename?
    filename = path.resolve(filename)
    generate_dependencies_for_file(filename)
    return
  else
    for filename in glob.sync(src_path + '/**/*.html')
      if not options.bower and filename.indexOf('/bower_components/') != -1
        continue
      generate_dependencies_for_file(filename)
    return
  #relative_path = path.relative src_path, filename
  #console.log relative_path
  #console.log input_file_path
